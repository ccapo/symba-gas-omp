program gen_ray_size
!---------------------------------------------------------------------------------
!				GEN_RAY_SIZE.F90
!---------------------------------------------------------------------------------
!
! Generates a SyMBA-style pl.in file with planetesimals distributed
! according to a power-law surface density and Rayleigh e-i distn.
! Also has option for one or more 'big guys' with orbital elements read in.
!
! May 22: This has feature forbidding
! plsmls to be initially within a sphere of radius 2 r_Hill from any 'big' guy
!
! March08: This version needs a file called 'size.dat' (generated by running
! 'drag_size_gen') with bins for distribution of drag radii
!
! April 20/2008:  Outputs file called 'dragradius.dat' with particle number,
! radius bin, and physical radius (km)
!
!---------------------------------------------------------------------------------
use module_swift
use module_interfaces
implicit none

! Parameters
integer(ik), parameter :: kbinmax = 1000

! Internal variables
integer(ik) :: nseed, time(8)
integer(ik), allocatable :: seed(:)

integer(ik) :: nbig,nplsml,npl
integer(ik) :: i,j,iseed,ialpha
real(rk) :: abeg,aend,pow,p2,factor,frac
real(rk) :: rmse,rmsi,random

real(rk) :: rhopl,mpl,rhpl,rpl
real(rk) :: apl,epl,ipl,capom,omega,capm
real(rk) :: gm,x,y,z,vx,vy,vz

real(rk) :: mbig(1000),rhsqbig(1000)
real(rk) :: xbig(1000),ybig(1000),zbig(1000)
real(rk) :: mplsml,rhplsml,rplsml
real(rk) :: aplsml,eplsml,iplsml
real(rk) :: r2(1000)

integer(ik) :: nplk(kbinmax)
real(rk) :: mplk(kbinmax),rhok(kbinmax),rdragk(kbinmax)
real(rk) :: rmsek(kbinmax),rmsik(kbinmax)

integer(ik) :: kbin,kk,k,kplsml

! Random seed based on current date and time
call random_seed(size = nseed)
allocate(seed(nseed))
call date_and_time(values = time)
seed = 79867_ik*sum(abs(time)) + 37_ik*[(i, i = 0, nseed - 1)]
call random_seed(put = seed)
!call random_seed(put = [123456789_ik, 987654321_ik] )

! Start with data for the plsml distn in semimajor axis
write(*,'(/,a)') 'For plsml distn, enter abeg (AU), aend (AU), power'
read(*,*) abeg, aend, pow
write(*,*) abeg, aend, pow
p2 = 2.0_rk - pow

!write(*,*) 'Enter random seed (large neg integer):'
read(*,*) iseed
!write(*,*) (/ ( i*seed, i = 1, nseed ) /) ! *** 06/17/08 *** CCC

! Now data for 'big guys'
write(*,'(/,a)') 'Input number of big guys and density (gm/cm^3):'
read(*,*) nbig, rhopl
write(*,*) nbig, rhopl

! Read in data from 'size.dat'
! Note we need to figure out how many plsmls before outputting any pl.in data
nplsml = 0
open(unit = 10, file = 'size.dat', status = 'old')

read(10,*) kbin

do k = 1, kbin

  read(10,*) kk, nplk(k), mplk(k), rhok(k), rdragk(k), rmsek(k), rmsik(k)
  nplsml = nplsml + nplk(k)

end do

! Now open the output file and write out vbles, starting with sun
npl = nbig + nplsml + 1
write(*,'(/,a,i9,/)') 'Total number of bodies including Sun: ', npl

open(unit = 8, file = 'pl.in', status = 'unknown')
write(8,*) npl

x = 0.0_rk
y = 0.0_rk
z = 0.0_rk
vx = 0.0_rk
vy = 0.0_rk
vz = 0.0_rk

write(8,'(1x,1pe22.15)') MSUN
write(8,'(3(1x,1pe22.15))') x,y,z
write(8,'(3(1x,1pe22.15))') vx,vy,vz

! Now add the big guys, reading in their masses and orb elements
ialpha = -1

if(nbig > 0) then

  do i = 1, nbig

    write(*,*) 'Enter mpl(MEARTH),apl,epl,ipl,capom,omega,capm(degs)'
    read(*,*) mpl,apl,epl,ipl,capom,omega,capm
    write(*,*) mpl,apl,epl,ipl,capom,omega,capm
    rpl = 5.221e-5_rk*(3.0_rk*mpl/rhopl)**(1.0_rk/3.0_rk)
    mpl = mpl*MEARTH
    rhpl = apl*(mpl/(3.0_rk*MSUN))**(1.0_rk/3.0_rk)
    ipl = ipl/DEGRAD
    capom = capom/DEGRAD
    omega = omega/DEGRAD
    capm =  capm/DEGRAD
    gm = MSUN + mpl
    call orbel_el2xv(gm,ialpha,apl,epl,ipl,capom,omega,capm,x,y,z,vx,vy,vz)

    write(8,'(3(1x,1pe22.15))') mpl,rhpl,rpl
    write(8,'(3(1x,1pe22.15))') x,y,z
    write(8,'(3(1x,1pe22.15))') vx,vy,vz
    mbig(i) = mpl
    rhsqbig(i) = rhpl*rhpl
    xbig(i) = x
    ybig(i) = y
    zbig(i) = z

  end do

end if

! Now write out plsml data and the rdrag data
open(unit = 12, file = 'drag_radius.dat', status = 'unknown')

kplsml = nbig + 2

do k = 1, kbin

  ! Compute radius of plsml in AU and convert mass to sim units
  rplsml = 5.221e-5_rk*(3.0_rk*mplk(k)/rhok(k))**(1.0_rk/3.0_rk)
  mplsml = mplk(k)*MEARTH
  rmse = rmsek(k)
  rmsi = rmsik(k)

  factor = 1.0_rk/max(1.0_rk, real(nplk(k) - 1, rk))

  do i = 1, nplk(k)

    frac = real(i - 1, rk)*factor
    aplsml = (abeg**p2 + frac*(aend**p2 - abeg**p2))**(1.0_rk/p2)
    rhplsml = aplsml*(mplsml/(3.0_rk*MSUN))**(1.0_rk/3.0_rk)
    eplsml = util_rayleigh(rmse, 0.0_rk, 1.0_rk)
    iplsml = util_rayleigh(rmsi, 0.0_rk, pi)
    !eplsml = 0.0_rk
    !iplsml = 0.0_rk
    gm = MSUN + mplsml

    if(nbig > 0) then

      r2 = 0.0_rk
      do while(any(r2(1:nbig) < 4.0_rk*rhsqbig(1:nbig)))

        capom = twopi*util_randomu()
        omega = twopi*util_randomu()
        capm = twopi*util_randomu()
        call orbel_el2xv(gm,ialpha,aplsml,eplsml,iplsml,capom,omega,capm,x,y,z,vx,vy,vz)

        ! Now check for proximity to big guys
        do j = 1, nbig

          r2(j) = (xbig(j) - x)*(xbig(j) - x) + (ybig(j) - y)*(ybig(j) - y) + (zbig(j) - z)*(zbig(j) - z)

        end do

      end do

    else

      capom = twopi*util_randomu()
      omega = twopi*util_randomu()
      capm = twopi*util_randomu()
      call orbel_el2xv(gm,ialpha,aplsml,eplsml,iplsml,capom,omega,capm,x,y,z,vx,vy,vz)

    end if

    write(8,'(3(1x,1pe22.15))') mplsml,rhplsml,rplsml
    write(8,'(3(1x,1pe22.15))') x,y,z
    write(8,'(3(1x,1pe22.15))') vx,vy,vz

    write(12,'(i9,2(1x,1pe22.15))') kplsml, rdragk(k), 0.84423_rk/(rdragk(k)*rhok(k))
    kplsml = kplsml + 1

  end do

end do

write(*,'(/,a)') 'Completed successfully'

stop
end program gen_ray_size
